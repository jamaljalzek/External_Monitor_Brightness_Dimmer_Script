/**** DIRECTIVE SECTION ****/

;; This is an AutoHotkey v2 script:
#Requires AutoHotkey v2.0-a (or later)

;; Enable warnings to assist with detecting common errors:
#Warn All

;; To access this file from another script in the same directory, add the line below to that script:
;; #Include "%A_ScriptDir%\Dimmer Settings Handler.ahk2"

/**** CLASS SECTION ****/

;; Public:
Class DimmingSettingsHandler
{
    ;; All private:
    PathOfSettingsFile := A_ScriptDir . "\Dimming Settings.txt"
    DimmingStartHour24 := 0
    DimmingStartMinute60 := 0
    DimmingDurationHour24 := 0
    DimmingDurationMinute60 := 0

    ;; Public (constructor):
    __New()
    {
        if (!this.DoesSettingsFileExist())
        {
            this.CreateDefaultSettingsFile()
        }
        this.InitializeFromSettingsFile()
    }

    ;; Private:
    DoesSettingsFileExist()
    {
        return FileExist(this.PathOfSettingsFile) != ""
    }

    ;; Private:
    CreateDefaultSettingsFile()
    {
        ;; For the first argument, to convert from PM to 24 hour format, add 12 to it.
        ;; For example, 6:00 PM = 6:00 + 12:00 = 18:00:
        this.SaveNewSettings(18, 0, 2, 0)
    }

    ;; Public:
    SaveNewSettings(dimmingStartHour24, dimmingStartMinute60, dimmingDurationHour24, dimmingDurationMinute60)
    {
        ;; Attempting to delete a file that doesn't exist throws an error instead of silently failing, so check first:
        if (this.DoesSettingsFileExist())
        {
            FileDelete(this.PathOfSettingsFile)
        }
        FileAppend(dimmingStartHour24, this.PathOfSettingsFile)
        FileAppend("`n" . dimmingStartMinute60, this.PathOfSettingsFile)
        FileAppend("`n" . dimmingDurationHour24, this.PathOfSettingsFile)
        FileAppend("`n" . dimmingDurationMinute60, this.PathOfSettingsFile)
    }

    ;; Private:
    InitializeFromSettingsFile()
    {
        settingsFileObject := FileOpen(this.PathOfSettingsFile, "R")
        this.DimmingStartHour24 := this.ReadNextSettingsValue(settingsFileObject)
        this.DimmingStartMinute60 := this.ReadNextSettingsValue(settingsFileObject)
        this.DimmingDurationHour24 := this.ReadNextSettingsValue(settingsFileObject)
        this.DimmingDurationMinute60 := this.ReadNextSettingsValue(settingsFileObject)
        settingsFileObject.Close()
    }

    ;; Private:
    ReadNextSettingsValue(settingsFileObject)
    {
        lineRead := settingsFileObject.ReadLine()
        if (IsNumber(lineRead))
        {
            return Number(lineRead)
        }
        this.ResetToDefaultSettingsAndReloadScript(settingsFileObject)
    }

    ;; Private:
    ResetToDefaultSettingsAndReloadScript(settingsFileObject)
    {
        MsgBox("ERROR: Invalid setting detected in settings file." . "`n" .
        "Script will reset to default settings and reload after closing this window.")
        ;; Attempting to replace the settings file while its associated File object is still open throws an error, so first close the File object:
        settingsFileObject.Close()
        this.CreateDefaultSettingsFile()
        Reload()
    }

    ;; Public:
    GetDimmingStartHH24()
    {
        return this.ConvertToTwoDigitString(this.DimmingStartHour24)
    }

    ;; Private:
    ConvertToTwoDigitString(numberToConvert)
    {
        if (numberToConvert < 10)
        {
            return "0" . numberToConvert
        }
        return "" . numberToConvert
    }

    ;; Public:
    GetDimmingStartMM60()
    {
        return this.ConvertToTwoDigitString(this.DimmingStartMinute60)
    }

    ;; Public:
    GetDimmingDurationTotalMilliseconds()
    {
        dimmingDurationTotalMinutes := this.DimmingDurationHour24 * 60 + this.DimmingDurationMinute60
        return dimmingDurationTotalMinutes * 60 * 1000
    }

    ;; Public:
    GetDimmingStartHour24()
    {
        return this.DimmingStartHour24
    }

    ;; Public:
    GetDimmingStartMinute60()
    {
        return this.DimmingStartMinute60
    }

    ;; Public:
    GetDimmingDurationHour24()
    {
        return this.DimmingDurationHour24
    }

    ;; Public:
    GetDimmingDurationMinute60()
    {
        return this.DimmingDurationMinute60
    }
}