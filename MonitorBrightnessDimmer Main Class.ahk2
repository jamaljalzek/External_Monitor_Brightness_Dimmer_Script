/**** DIRECTIVE SECTION ****/

;; This is an AutoHotkey v2 script:
#Requires AutoHotkey v2.0-a (or later)

;; Enable warnings to assist with detecting common errors:
#Warn All

#Include "%A_ScriptDir%\DimmerSettingsHandler Class.ahk2"

#Include "%A_ScriptDir%\DimmerSettingsWindow Class.ahk2"

#Include "%A_ScriptDir%\MonitorBrightnessChanger Class.ahk2"

/**** EXECUTION SECTION ****/

MonitorBrightnessDimmer.Main()

/**** CLASS SECTION ****/

;; Public:
Class MonitorBrightnessDimmer
{
    ;; Public:
    Static Main()
    {
        dimmerSettings := DimmerSettingsHandler()
        DimmerSettingsWindow.AddNewSettingsWindowToScriptTrayMenu(dimmerSettings)
        MonitorBrightnessDimmer.SleepUntilDimmingStartTime(dimmerSettings)
        MonitorBrightnessDimmer.DimToMinimumBrightness(dimmerSettings)
    }

    ;; Private:
    Static SleepUntilDimmingStartTime(dimmerSettings)
    {
        todaysDimmingStartYYYYMMDDHH24MISS := dimmerSettings.GetTodaysDimmingStartYYYYMMDDHH24MISS()
        secondsUntilDimmingStart := DateDiff(todaysDimmingStartYYYYMMDDHH24MISS, A_Now, "Seconds")
        millisecondsUntilDimmingStart := secondsUntilDimmingStart * 1000
        Sleep(millisecondsUntilDimmingStart)
    }

    ;; Private:
    Static DimToMinimumBrightness(dimmerSettings)
    {
        brightnessChanger := MonitorBrightnessChanger()
        ;; In case the minimum possible brightness value is greater than 0:
        brightnessToDimBy := brightnessChanger.GetCurrentBrightness() - brightnessChanger.GetMinimumBrightness()
        ;; Integer division (//) is used below:
        millisecondsPerBrightnessDecrement := dimmerSettings.GetDimmingDurationTotalMilliseconds() // brightnessToDimBy
        MonitorBrightnessDimmer.BeginDimmingBrightness(brightnessChanger, millisecondsPerBrightnessDecrement)
    }

    ;; Private:
    Static BeginDimmingBrightness(brightnessChanger, millisecondsPerBrightnessDecrement)
    {
        while (brightnessChanger.GetCurrentBrightness() > brightnessChanger.GetMinimumBrightness())
        {
            brightnessChanger.DecrementBrightness()
            Sleep(millisecondsPerBrightnessDecrement)
        }
    }
}